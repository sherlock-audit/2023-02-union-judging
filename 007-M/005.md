chaduke

medium

# UToken.borrow() might break the debitCeiling due to not considering the fee

## Summary
``UToken.borrow()`` might break the ``debitCeiling`` due to not considering the fee. As a result, after ``UToken.borrow()`` completes, it is possible that ``totalBorrows > debtCeiling``.

## Vulnerability Detail
The reason that ``UToken.borrow()`` might break the ``debitCeiling`` is because it only checks ``amount`` against the remaining debt ceiling: 
```javascript
function getRemainingDebtCeiling() public view override returns (uint256) {
        return debtCeiling >= totalBorrows ? debtCeiling - totalBorrows : 0;
    }

function borrow(address to, uint256 amount) external override onlyMember(msg.sender) whenNotPaused nonReentrant {
        IAssetManager assetManagerContract = IAssetManager(assetManager);
        if (amount < minBorrow) revert AmountLessMinBorrow();
        if (amount > getRemainingDebtCeiling()) revert AmountExceedGlobalMax();
```

However, after the completion of ``UToken.borrow()``, the ``totalBorrows`` might be increased by ``amount+ fee``. As a result, it is possible that after the completion of ``UToken.borrow()``, ``totalBorrows`` might exceed ``debtCeiling`` by the amount of ``fee``.

```javascript
        fee = calculatingFee(actualAmount);
        uint256 accountBorrowsNew = borrowedAmount + actualAmount + fee;
        uint256 totalBorrowsNew = totalBorrows + actualAmount + fee;

// Update internal balances
        accountBorrows[msg.sender].principal += actualAmount + fee;
        uint256 newPrincipal = getBorrowed(msg.sender);
        accountBorrows[msg.sender].interest = accountBorrowsNew - newPrincipal;
        accountBorrows[msg.sender].interestIndex = borrowIndex;
        totalBorrows = totalBorrowsNew;
```

Consider the example: ``debtCeiling`` = 100,000,000, and ``totalBorrowed = 80,000,000``, ``originationFee = 0.00001WAD (0.001%)``, suppose ``amount = 20,000,000``.

Since ``amount+totalBorrowed = 100,000,000 = debtCeiling``, the ``borrow()`` function will pass the debtCeiling check.

However, after the ``borrow()`` function completes, if ``actualAmount = amount``, then it is possible that the new ``totalBorrows = 80,000,000+20,000,000+fee = 100,000,000+20,000,000*0.00001WAD/WAD = 100,000,000+200 = 100,000,200``

As a result, the final ``totalBorrows`` will exceed ``debtCeiling`` by 200. 

## Impact
``UToken.borrow()`` might break the ``debitCeiling`` due to not considering the fee. As a result, after ``UToken.borrow()`` completes, it is possible that ``totalBorrows > debtCeiling``.

Depending the amount of fee, this might make the pool not healthy, and might lead to a bank run if things are not going well.

## Code Snippet
[https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/market/UToken.sol#L525-L571](https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/market/UToken.sol#L525-L571)

## Tool used
Remix
Manual Review

## Recommendation
Perform the debitCeiling Check using the final totalBorrow, in which both ``actualAmount`` and ``fee`` are considered. Checking in the beginning if premature, the real amount might be small (due to available funds) or bigger (due to fee).
```diff
function borrow(address to, uint256 amount) external override onlyMember(msg.sender) whenNotPaused nonReentrant {
        IAssetManager assetManagerContract = IAssetManager(assetManager);
        if (amount < minBorrow) revert AmountLessMinBorrow();
-        if (amount > getRemainingDebtCeiling()) revert AmountExceedGlobalMax();

        // Calculate the origination fee
        uint256 fee = calculatingFee(amount);

        if (borrowBalanceView(msg.sender) + amount + fee > maxBorrow) revert AmountExceedMaxBorrow();
        if (checkIsOverdue(msg.sender)) revert MemberIsOverdue();
        if (amount > assetManagerContract.getLoanableAmount(underlying)) revert InsufficientFundsLeft();
        if (!accrueInterest()) revert AccrueInterestFailed();

        uint256 borrowedAmount = borrowBalanceStoredInternal(msg.sender);

        // Initialize the last repayment date to the current block number
        if (getLastRepay(msg.sender) == 0) {
            accountBorrows[msg.sender].lastRepay = getBlockNumber();
        }

        // Withdraw the borrowed amount of tokens from the assetManager and send them to the borrower
        uint256 remaining = assetManagerContract.withdraw(underlying, to, amount);
        if (remaining > amount) revert WithdrawFailed();
        uint256 actualAmount = amount - remaining;

        fee = calculatingFee(actualAmount);
        uint256 accountBorrowsNew = borrowedAmount + actualAmount + fee;
        uint256 totalBorrowsNew = totalBorrows + actualAmount + fee;
+       if (totalBorroweNew > debtCeiling) revert AmountExceedGlobalMax();


        // Update internal balances
        accountBorrows[msg.sender].principal += actualAmount + fee;
        uint256 newPrincipal = getBorrowed(msg.sender);
        accountBorrows[msg.sender].interest = accountBorrowsNew - newPrincipal;
        accountBorrows[msg.sender].interestIndex = borrowIndex;
        totalBorrows = totalBorrowsNew;

        // The origination fees contribute to the reserve and not to the
        // uDAI minters redeemable amount.
        totalReserves += fee;

        // Call update locked on the userManager to lock this borrowers stakers. This function
        // will revert if the account does not have enough vouchers to cover the borrow amount. ie
        // the borrower is trying to borrow more than is able to be underwritten
        IUserManager(userManager).updateLocked(msg.sender, (actualAmount + fee).toUint96(), true);

        emit LogBorrow(msg.sender, to, actualAmount, fee);
    }
```
