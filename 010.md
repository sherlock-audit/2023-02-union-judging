RaymondFam

medium

# Missing approve to zero could cause certain token transfer to fail

## Summary
Some tokens, e.g USDT, do not work when changing the allowance from an existing non-zero allowance value. For instance, Tether (USDT)'s `approve()` will revert if the current approval has not been set to zero, serving to protect against front-running changes of approvals.

## Vulnerability Detail
In the protocol, all functions using `safeIncreaseAllowance()` must first be approved by zero. These are `mapTokenToAToken()` in AaveV3Adapter.sol, `_increaseAllowance()` in AssetManager.sol, `_depositToAssetManager()` in Utoken.sol, `bridge()` in OpConnector.sol, and `stake()` in UserManager.sol.

## Impact
Otherwise, these functions are going to revert every time they encounter such kind of tokens that might have a remaining allowance associated.

## Code Snippet
[File: AaveV3Adapter.sol#L188-L199](https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/asset/AaveV3Adapter.sol#L188-L199)

```solidity
    function mapTokenToAToken(address tokenAddress) external onlyAdmin {
        LendingPool3.ReserveData memory reserveData = lendingPool.getReserveData(tokenAddress);
        IERC20Upgradeable token = IERC20Upgradeable(tokenAddress);

        address spender = address(lendingPool);
        uint256 currentAllowance = token.allowance(address(this), spender);
        if (currentAllowance < type(uint256).max) {
            token.safeIncreaseAllowance(spender, type(uint256).max - currentAllowance);
        }

        tokenToAToken[tokenAddress] = reserveData.aTokenAddress;
    }
```
[File: AssetManager.sol#L565-L570](https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/asset/AssetManager.sol#L565-L570)

```solidity
    function _increaseAllowance(IERC20Upgradeable token, address spender, uint256 amount) internal {
        uint256 currentAllowance = token.allowance(address(this), spender);
        if (currentAllowance < amount) {
            token.safeIncreaseAllowance(spender, amount - currentAllowance);
        }
    }
```
[File: UToken.sol#L831-L840](https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/market/UToken.sol#L831-L840)

```solidity
    function _depositToAssetManager(uint256 amount) internal {
        IERC20Upgradeable assetToken = IERC20Upgradeable(underlying);

        uint256 currentAllowance = assetToken.allowance(address(this), assetManager);
        if (currentAllowance < amount) {
            assetToken.safeIncreaseAllowance(assetManager, amount - currentAllowance);
        }

        if (!IAssetManager(assetManager).deposit(underlying, amount)) revert DepositToAssetManagerFailed();
    }
```
[File: OpConnector.sol#L31-L49](https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/token/OpConnector.sol#L31-L49)

```solidity
    function bridge() external {
        uint256 amount = IERC20(l1Token).balanceOf(address(this));
        require(amount > 0, "OpConnector: Amount zero");
        uint256 currentAllowance = IERC20(l1Token).allowance(address(this), l1BridgeAddr);
        if (currentAllowance < type(uint256).max) {
            // Approve the allowance once for all
            IERC20(l1Token).safeIncreaseAllowance(l1BridgeAddr, type(uint256).max - currentAllowance);
        }
        IL1StandardBridge(l1BridgeAddr).depositERC20To(
            l1Token,
            l2Token,
            target,
            amount,
            1000000, // within the free gas limit amount
            ""
        );

        emit LogDeposit(msg.sender, target, amount);
    }
```
[File: UserManager.sol#L681-L702](https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/user/UserManager.sol#L681-L702)

```solidity
    function stake(uint96 amount) public whenNotPaused nonReentrant {
        IERC20Upgradeable erc20Token = IERC20Upgradeable(stakingToken);

        comptroller.withdrawRewards(msg.sender, stakingToken);

        Staker storage staker = stakers[msg.sender];

        if (staker.stakedAmount + amount > maxStakeAmount) revert StakeLimitReached();

        _updateStakedCoinAge(msg.sender, staker);
        staker.stakedAmount += amount;
        totalStaked += amount;

        erc20Token.safeTransferFrom(msg.sender, address(this), amount);
        uint256 currentAllowance = erc20Token.allowance(address(this), assetManager);
        if (currentAllowance < amount) {
            erc20Token.safeIncreaseAllowance(assetManager, amount - currentAllowance);
        }

        if (!IAssetManager(assetManager).deposit(stakingToken, amount)) revert AssetManagerDepositFailed();
        emit LogStake(msg.sender, amount);
    }
```
## Tool used

Manual Review

## Recommendation
Consider approving 0 first prior to using `safeIncreaseAllowance()` to set the value of allowances.

For example, the first instance in the Code Snippet may be refactored as follows:

```diff
    function mapTokenToAToken(address tokenAddress) external onlyAdmin {
        LendingPool3.ReserveData memory reserveData = lendingPool.getReserveData(tokenAddress);
        IERC20Upgradeable token = IERC20Upgradeable(tokenAddress);

        address spender = address(lendingPool);
        uint256 currentAllowance = token.allowance(address(this), spender);
        if (currentAllowance < type(uint256).max) {
+            token.safeApprove(spender, 0);
            token.safeIncreaseAllowance(spender, type(uint256).max - currentAllowance);
        }

        tokenToAToken[tokenAddress] = reserveData.aTokenAddress;
    }
```