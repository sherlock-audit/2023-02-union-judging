chaduke

medium

# A malicious staker can help millions to become members with minimal fees

## Summary
A malicious staker can help millions to become members with minimal fees. This is possible because:  1) It only requires non-zero staked amount; 2) The staked amount can be reused for as many vouches and for as many new users as possible. 

## Vulnerability Detail
Let's see how a malicious staker Bob can only use 5 staked tokens to help millions to become members and enjoy membership benefit. 

1) First of all, membership is good as a member can enjoy more rewards due to a higher multiplier: 

```javascript
function withdrawRewards(address account, address token) external override whenNotPaused returns (uint256) {
        IUserManager userManager = _getUserManager(token);

        // Lookup account state from UserManager
        (UserManagerAccountState memory user, Info memory userInfo, uint256 pastBlocks) = _getUserInfo(
            userManager,
            account,
            token,
            0
        );

        // Lookup global state from UserManager
        uint256 globalTotalStaked = userManager.globalTotalStaked();

        uint256 amount = _calculateRewardsByBlocks(account, token, pastBlocks, userInfo, globalTotalStaked, user);

        // update the global states
        gInflationIndex = _getInflationIndexNew(globalTotalStaked, block.number - gLastUpdatedBlock);
        gLastUpdatedBlock = block.number;
        users[account][token].updatedBlock = block.number;
        users[account][token].inflationIndex = gInflationIndex;
        if (unionToken.balanceOf(address(this)) >= amount && amount > 0) {
            unionToken.safeTransfer(account, amount);
            users[account][token].accrued = 0;
            emit LogWithdrawRewards(account, amount);

            return amount;
        } else {
            users[account][token].accrued = amount;
            emit LogWithdrawRewards(account, 0);

            return 0;
        }
    }




function _calculateRewardsByBlocks(
        address account,
        address token,
        uint256 pastBlocks,
        Info memory userInfo,
        uint256 totalStaked,
        UserManagerAccountState memory user
    ) internal view returns (uint256) {
        uint256 startInflationIndex = users[account][token].inflationIndex;

        if (user.effectiveStaked == 0 || totalStaked == 0 || startInflationIndex == 0 || pastBlocks == 0) {
            return 0;
        }

        uint256 rewardMultiplier = _getRewardsMultiplier(user);

        uint256 curInflationIndex = _getInflationIndexNew(totalStaked, pastBlocks);

        if (curInflationIndex < startInflationIndex) revert InflationIndexTooSmall();

        return
            userInfo.accrued +
            (curInflationIndex - startInflationIndex).wadMul(user.effectiveStaked).wadMul(rewardMultiplier);
    }


    function _getRewardsMultiplier(UserManagerAccountState memory user) internal pure returns (uint256) {
        if (user.isMember) {
            if (user.effectiveStaked == 0) {
                return memberRatio;
            }

            uint256 lendingRatio = user.effectiveLocked.wadDiv(user.effectiveStaked);

            return lendingRatio + memberRatio;
        } else {
            return nonMemberRatio;
        }
    }
```

2) Second, to become a member, one needs to have five vouchers, each of which has ``staker.stakedAmount > 0``

```javascript
function _validateNewMember(address newMember) internal {
        if (stakers[newMember].isMember) revert NoExistingMember();

        uint256 count = 0;
        uint256 vouchersLength = vouchers[newMember].length;
        Vouch memory vouch;
        Staker memory staker;

        // Loop through all the vouchers to count how many active vouches there
        // are that are greater than 0. Vouch is the min of stake and trust
        for (uint256 i = 0; i < vouchersLength; i++) {
            vouch = vouchers[newMember][i];
            staker = stakers[vouch.staker];
            if (staker.stakedAmount > 0) count++;
            if (count >= effectiveCount) break;
        }

        if (count < effectiveCount) revert NotEnoughStakers();

        stakers[newMember].isMember = true;
    }
}


  function registerMember(address newMember) public virtual whenNotPaused {
        _validateNewMember(newMember);

        IUnionToken(unionToken).burnFrom(msg.sender, newMemberFee);

        emit LogRegisterMember(msg.sender, newMember);
    }
```

3) Malicious Bob can create five staker accounts, each of which only stakes 1 token. Note that there is no minium stake amount requirement. 

```javascript
function stake(uint96 amount) public whenNotPaused nonReentrant {
        IERC20Upgradeable erc20Token = IERC20Upgradeable(stakingToken);

        comptroller.withdrawRewards(msg.sender, stakingToken);

        Staker storage staker = stakers[msg.sender];

        if (staker.stakedAmount + amount > maxStakeAmount) revert StakeLimitReached();

        _updateStakedCoinAge(msg.sender, staker);
        staker.stakedAmount += amount;
        totalStaked += amount;

        erc20Token.safeTransferFrom(msg.sender, address(this), amount);
        uint256 currentAllowance = erc20Token.allowance(address(this), assetManager);
        if (currentAllowance < amount) {
            erc20Token.safeIncreaseAllowance(assetManager, amount - currentAllowance);
        }

        if (!IAssetManager(assetManager).deposit(stakingToken, amount)) revert AssetManagerDepositFailed();
        emit LogStake(msg.sender, amount);
    }
```

4) For any user, Alice, who likes to become a member, Bob can offer his help (possibly charging a fee) to trust her with five vouchers using his five staker accounts by calling the ``updateTrust()`` function. Note that there is no minimum requirement for the ``trust`` amount. There is also no check that the same staked amount cannot be reused across different vouchers. 

[https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/user/UserManager.sol#L532-L571](https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/user/UserManager.sol#L532-L571)

5) Bob can repeat step 4 to help millions to become member with only 5 tokens, one token in each staker's account. 

## Impact
A malicious staker can help millions to become members with minimal fees. 

## Code Snippet
See above

## Tool used
Remix

Manual Review

## Recommendation
1) There should be a minimum ``stake`` and ``trust`` checks.
2) The ``stakedAmount`` for the staker should be consumed when a new member is introduced by the vouchers issued by the staker. In this way, the staker cannot reuse the same ``stakedAmount`` to refer as many people as he wants.
3) Membership needs to re-verified each time a user needs to withdraw his/her rewards since there might not be enough active vouchers later as stakers might unstake their tokens from the contract. 

