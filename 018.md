RaymondFam

medium

# Front-running of initialization

## Summary
The protocol's initialization functions are generally unprotected with no access controls in place, and can be front-run by anyone.

## Vulnerability Detail
These are:
- `__Controller_init()` in Controller.sol
- `__AaveV3Adapter_init()` in AaveV3Adapter.sol
- `__AssetManager_init()` in AssetManager.sol
- `__PureTokenAdapter_init()` in PureTokenAdapter.sol
- `__MarketRegistry_init()` in MarketRegistry.sol
- `__UToken_init()` in Utoken.sol
- `__Comptroller_init()` in Comptroller.sol
- `__UserManager_init()` in UserManager.sol

## Impact
The init parameters can be set only once in the functions above because of `initializer` and will be permanently determined by the attacker unless there are setter functions catered for the parameters involved. The situations could be worse if the initializations were to entail setting default admin and critical roles to the contract.

## Code Snippet
[File: Controller.sol#L95-L100](https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/Controller.sol#L95-L100)

```solidity 
   function __Controller_init(address admin_) public initializer {
        _paused = false;
        admin = admin_;
        __UUPSUpgradeable_init();
        pauseGuardian = admin_;
    }
```
[File: AaveV3Adapter.sol#L67-L77](https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/asset/AaveV3Adapter.sol#L67-L77)

```solidity
    function __AaveV3Adapter_init(
        address admin,
        address _assetManager,
        LendingPool3 _lendingPool,
        AMarket3 _market
    ) public initializer {
        Controller.__Controller_init(admin);
        assetManager = _assetManager;
        lendingPool = _lendingPool;
        market = _market;
    }
```
[File: AssetManager.sol#L108-L112](https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/asset/AssetManager.sol#L108-L112)

```solidity
    function __AssetManager_init(address admin, address _marketRegistry) external initializer {
        Controller.__Controller_init(admin);
        ReentrancyGuardUpgradeable.__ReentrancyGuard_init();
        marketRegistry = _marketRegistry;
    }
```
[File: PureTokenAdapter.sol#L36-L39](https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/asset/PureTokenAdapter.sol#L36-L39)

```solidity
    function __PureTokenAdapter_init(address admin, address _assetManager) public initializer {
        Controller.__Controller_init(admin);
        assetManager = _assetManager;
    }
```
[File: MarketRegistry.sol#L54-L56](https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/market/MarketRegistry.sol#L54-L56)

```solidity
    function __MarketRegistry_init(address admin) public initializer {
        Controller.__Controller_init(admin);
    }
```
[File: UToken.sol#L243-L274](https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/market/UToken.sol#L243-L274)

```solidity
    function __UToken_init(
        string memory name_,
        string memory symbol_,
        address underlying_,
        uint256 initialExchangeRateMantissa_,
        uint256 reserveFactorMantissa_,
        uint256 originationFee_,
        uint256 originationFeeMax_,
        uint256 debtCeiling_,
        uint256 maxBorrow_,
        uint256 minBorrow_,
        uint256 overdueBlocks_,
        address admin_
    ) public initializer {
        if (initialExchangeRateMantissa_ == 0) revert InitExchangeRateNotZero();
        if (reserveFactorMantissa_ > RESERVE_FACTORY_MAX_MANTISSA) revert ReserveFactoryExceedLimit();
        Controller.__Controller_init(admin_);
        ERC20Upgradeable.__ERC20_init(name_, symbol_);
        ERC20PermitUpgradeable.__ERC20Permit_init(name_);
        ReentrancyGuardUpgradeable.__ReentrancyGuard_init();
        underlying = underlying_;
        originationFee = originationFee_;
        originationFeeMax = originationFeeMax_;
        debtCeiling = debtCeiling_;
        maxBorrow = maxBorrow_;
        minBorrow = minBorrow_;
        overdueBlocks = overdueBlocks_;
        initialExchangeRateMantissa = initialExchangeRateMantissa_;
        reserveFactorMantissa = reserveFactorMantissa_;
        accrualBlockNumber = getBlockNumber();
        borrowIndex = WAD;
    }
```
[File: Comptroller.sol#L113-L127](https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/token/Comptroller.sol#L113-L127)

```solidity
    function __Comptroller_init(
        address admin,
        address unionToken_,
        address marketRegistry_,
        uint256 _halfDecayPoint
    ) public initializer {
        Controller.__Controller_init(admin);

        gInflationIndex = INIT_INFLATION_INDEX;
        gLastUpdatedBlock = block.number;

        unionToken = IERC20Upgradeable(unionToken_);
        marketRegistry = IMarketRegistry(marketRegistry_);
        halfDecayPoint = _halfDecayPoint;
    }
```
[File: UserManager.sol#L309-L332](https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/user/UserManager.sol#L309-L332)

```solidity
    function __UserManager_init(
        address assetManager_,
        address unionToken_,
        address stakingToken_,
        address comptroller_,
        address admin_,
        uint256 maxOverdueBlocks_,
        uint256 effectiveCount_,
        uint256 maxVouchers_,
        uint256 maxVouchees_
    ) public initializer {
        Controller.__Controller_init(admin_);
        ReentrancyGuardUpgradeable.__ReentrancyGuard_init();
        comptroller = IComptroller(comptroller_);
        assetManager = assetManager_;
        unionToken = unionToken_;
        stakingToken = stakingToken_;
        newMemberFee = 1 ether;
        maxStakeAmount = 10_000e18;
        maxOverdueBlocks = maxOverdueBlocks_;
        effectiveCount = effectiveCount_;
        maxVouchers = maxVouchers_;
        maxVouchees = maxVouchees_;
    }
```
## Tool used

Manual Review

## Recommendation
As per Openzeppelin's recommendation:

https://forum.openzeppelin.com/t/uupsupgradeable-vulnerability-post-mortem/15680/6

The guidelines are now to prevent front-running of initialize() on an implementation contract, by adding an empty constructor with the initializer modifier. Hence, the implementation contract gets initialized atomically upon deployment.

This feature is readily incorporated in the Solidity Wizard since the UUPS vulnerability discovery. You would just need to check UPGRADEABILITY to have a specific constructor code block added to the contract.

```solidity
    /// @custom:oz-upgrades-unsafe-allow constructor
    constructor() {
        _disableInitializers();
    }
```
Alternatively, consider:
● adding access controls to initialize(), or
● ensuring that the documentation clearly warns users about incorrect initialization.

Long term, avoid initialization outside of the constructor. If that is not possible, ensure that the underlying risks of initialization are documented and properly tested.