chaduke

unlabeled

# [Medium] UserManager._cancelVouchInternal() will remove a vouchee entry from the staker's vouchee list  using the wrong index

## Summary
``UserManager._cancelVouchInternal()`` will remove a vouchee entry from the staker's vouchee list  using the wrong index. As a result, the wrong vouchee entry will be removed, affecting another borrower's credit. 

## Vulnerability Detail
``UserManager._cancelVouchInternal()`` attemps to remove a vouchee from both the borrower's list and the staker's list. However, the same index is used for both cases (L584 and L609):

[https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/user/UserManager.sol#L583-L623](https://github.com/sherlock-audit/2023-02-union/blob/main/union-v2-contracts/contracts/user/UserManager.sol#L583-L623)
The index used to remove vouchee from the staker's list is wrong.

## Impact
A wrong entry will be removed from the staker's vouchee list, potentially affecting other borrower's credit for borrowing. 

## Code Snippet
See above

## Tool used
Remix

Manual Review

## Recommendation
We need to use the correct index for removing the vouchee from the staker's list.
```diff
 function _cancelVouchInternal(address staker, address borrower) internal {
        Index memory removeVoucherIndex = voucherIndexes[borrower][staker];
        if (!removeVoucherIndex.isSet) revert VoucherNotFound();

        // Check that the locked amount for this vouch is 0
        Vouch memory vouch = vouchers[borrower][removeVoucherIndex.idx];
        if (vouch.locked > 0) revert LockedStakeNonZero();

        // Remove borrower from vouchers array by moving the last item into the position
        // of the index being removed and then poping the last item off the array
        {
            // Cache the last voucher
            Vouch memory lastVoucher = vouchers[borrower][vouchers[borrower].length - 1];
            // Move the lastVoucher to the index of the voucher we are removing
            vouchers[borrower][removeVoucherIndex.idx] = lastVoucher;
            // Pop the last vouch off the end of the vouchers array
            vouchers[borrower].pop();
            // Delete the voucher index for this borrower => staker pair
            delete voucherIndexes[borrower][staker];
            // Update the last vouchers coresponsing Vouchee item
            uint128 voucheeIdx = voucherIndexes[borrower][lastVoucher.staker].idx;
            vouchees[staker][voucheeIdx].voucherIndex = removeVoucherIndex.idx.toUint96();
        }

        // Update the vouchee entry for this borrower => staker pair
        {
-         Index memory removeVoucheeIndex = voucheeIndexes[borrower][staker];
+        Index memory removeVoucheeIndex = voucheeIndexes[staker][borrower];

            // Cache the last vouchee
            Vouchee memory lastVouchee = vouchees[staker][vouchees[staker].length - 1];
            // Move the last vouchee to the index of the removed vouchee
            vouchees[staker][removeVoucheeIndex.idx] = lastVouchee;
            // Pop the last vouchee off the end of the vouchees array
            vouchees[staker].pop();
            // Delete the vouchee index for this borrower => staker pair
            delete voucheeIndexes[borrower][staker];
            // Update the vouchee indexes to the new vouchee index
            voucheeIndexes[lastVouchee.borrower][staker].idx = removeVoucheeIndex.idx;
        }

        emit LogCancelVouch(staker, borrower);
    }
```
